import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext {
        kotlin_version = '1.7.10'
        compose_version = '1.2.1'
        compose_compiler_version = '1.3.0-beta01'
        detekt_version = "1.21.0"
        ksp_version = "$kotlin_version-1.0.6"
        koin_version = "3.2.0"
        koin_ksp_version= "1.0.1"
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
    repositories {
        mavenCentral()
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false

    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false

    id 'io.gitlab.arturbosch.detekt' version "$detekt_version"
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        toolVersion = "$detekt_version"
        buildUponDefaultConfig = true
        allRules = true
        config = files("$rootDir/config/detekt/detekt.yml")
        ignoreFailures = false
        parallel = true
    }

    tasks.withType(Detekt).configureEach {
        autoCorrect = true
        it.reports {
            txt.required.set(true)
            xml.required.set(false)
            html.required.set(false)
            sarif.required.set(false)
        }
        exclude("org/koin/ksp/generated/**")
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
