import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.serialization apply false
    alias libs.plugins.kotlin.jvm apply false
    alias libs.plugins.detekt
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        toolVersion = libs.versions.detekt.get()
        buildUponDefaultConfig = true
        allRules = true
        config = files("$rootDir/config/detekt/detekt.yml")
        ignoreFailures = false
        parallel = true
    }

    tasks.withType(Detekt).configureEach {
        autoCorrect = true
        it.reports {
            txt.required.set(true)
            xml.required.set(false)
            html.required.set(false)
            sarif.required.set(false)
        }
        exclude("org/koin/ksp/generated/**")
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${libs.versions.detekt.get()}"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
